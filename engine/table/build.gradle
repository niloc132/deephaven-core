import groovy.json.JsonBuilder

plugins {
    id 'java-library'
}

description 'Engine Table: Implementation and closely-coupled utilities'

sourceSets {
    main {
        resources {
            srcDir 'groovy'
            include 'core/*.groovy'
        }
        resources {
            srcDir 'python'
            include 'core/*.py'
        }
        resources {
            srcDir 'src/main/resources'
            include '**'
        }
    }
}

dependencies {
    api project(':Util'),
            project(':TableLogger'),
            project(':engine-stringset'),
            project(':engine-base'),
            project(':deephaven-jpy'),
            project(':deephaven-jpy-ext')

    implementation project(':CompilerTools'),
            project(':DHProcess'),
            project(':engine-function'),
            project(':engine-tuple'),
            'com.github.f4b6a3:uuid-creator:3.6.0',
            "org.eclipse.jgit:org.eclipse.jgit:$Classpaths.JGIT_VERSION",
            'com.tdunning:t-digest:3.2'

    implementation project(':plugin')

    Classpaths.inheritGroovy(project, 'groovy', 'implementation')
    Classpaths.inheritGroovy(project, 'groovy-json', 'implementation')
    Classpaths.inheritJavaParser(project)
    Classpaths.inheritAutoService(project)

    Classpaths.inheritImmutables(project)

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    runtimeOnly project(':engine-tuplesource')

    testImplementation TestTools.projectDependency(project, 'Base'),
            TestTools.projectDependency(project, 'Util'),
            TestTools.projectDependency(project, 'engine-chunk'),
            TestTools.projectDependency(project, 'engine-rowset'),
            project(':BenchmarkSupport'),
            project(':extensions-csv'),
            project(':extensions-parquet-table'),
            'junit:junit:4.13.2'

    Classpaths.inheritJUnitPlatform(project)
    Classpaths.inheritAssertJ(project)

    testRuntimeOnly project(':log-to-slf4j'),
            project(path: ':configs'),
            project(path: ':test-configs')
    Classpaths.inheritSlf4j(project, 'slf4j-simple', 'testRuntimeOnly')
}

spotless {
    java {
        targetExclude(
                '**/generated/**',
                '**/*Boolean*.java',
                '**/*Byte*.java',
                '**/*Char*.java',
                '**/*Short*.java',
                '**/*Int*.java',
                '**/*Long*.java',
                '**/*Float*.java',
                '**/*Double*.java',
                '**/*Object*.java',
                '**/*StateManager.java',
                'src/main/java/io/deephaven/engine/table/impl/SymbolTableCombiner.java',
                'src/main/java/io/deephaven/libs/GroovyStaticImports.java',
                'src/test/java/**/*Sample.java'
        )
    }
}

test {
    maxParallelForks = Integer.parseInt(findProperty('maxParallelForks') as String ?: '1')
    forkEvery = Integer.parseInt(findProperty('forkEvery') as String ?: '1')
    //  For now, if you apply @Category(ParallelTest.class) to tests which are not huge CPU/RAM hogs, you can get parallelism
    //  If you have CPU/RAM-heavy tasks that you don't want gumming up :engine-table:test runs, apply @Category(SerialTest.class) instead
    //  (note that the above only works for junit 4 tests; see the documentation on SerialTest class and others for porting instructions)

    def props = project.properties.subMap(["jpy.pythonLib", "jpy.jpyLib", "jpy.jdlLib"])
    systemProperties props
}

// Adds a custom testParallel task which is allowed to run tests in parallel.
// Add @Category(ParallelTest.class) to have your tests run in parallel
// Note: Supports JUnit4 or greater only (you use @Test annotations to mark test methods).

TestTools.addEngineParallelTest(project)
TestTools.addEngineSerialTest(project)
TestTools.addEngineOutOfBandTest(project)

def maxHeapSize = findProperty('maxHeapSize') as String ?: '6g'

tasks.testParallel.maxHeapSize = maxHeapSize
tasks.testSerial.maxHeapSize = maxHeapSize
tasks.testOutOfBand.maxHeapSize = maxHeapSize
tasks.test.maxHeapSize = maxHeapSize

def tier4CompilationExcludes = [
        'io.deephaven.engine.table.impl.by.ssmpercentile.DoublePercentileTypeMedianHelper':['setResult'],
        'io.deephaven.engine.table.impl.by.ssmpercentile.FloatPercentileTypeMedianHelper':['setResult'],
        'io.deephaven.engine.table.impl.by.ssmpercentile.SsmChunkedPercentileOperator':['addChunk'],
        'io.deephaven.engine.table.impl.by.IncrementalChunkedOperatorAggregationStateManager':['buildTable'],
        'io.deephaven.engine.table.impl.RightIncrementalChunkedCrossJoinStateManager':['addToIndex', 'decorationProbe'],
        'io.deephaven.engine.table.impl.IncrementalChunkedNaturalJoinStateManager':['buildTable', 'decorationProbe'],
]
def createCompilerDirectives = tasks.register('createCompilerDirectives') {
    def c2ExcludesFile = project.layout.buildDirectory.file('c2-excludes.txt')
    it.outputs.file(c2ExcludesFile)

    doFirst {
        def excludesAsList = []
        tier4CompilationExcludes.forEach({type, methods ->
            methods.forEach {method ->
                excludesAsList += "${type}::${method}"
            }
        })
        def builder = new JsonBuilder()
        builder([{
                     match excludesAsList
                     c2 {
                         Exclude true
                     }
                 }])
        c2ExcludesFile.get().asFile.text = builder.toPrettyString()
    }
}
tasks.testOutOfBand.jvmArgs '-XX:Tier4CompileThreshold=1000'
//tasks.testOutOfBand.inputs.files createCompilerDirectives.get().outputs.files

//tasks.testOutOfBand.jvmArgs '-XX:+PrintCompilation'
//tasks.testOutOfBand.jvmArgs '-XX:+PrintFlagsFinal'
//tasks.testOutOfBand.jvmArgs '-XX:+UnlockDiagnosticVMOptions'
//tasks.testOutOfBand.jvmArgs "-XX:CompilerDirectivesFile=${createCompilerDirectives.get().outputs.files.singleFile}"
